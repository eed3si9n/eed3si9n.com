---
title:       "Bazel + Scalafix ã‚’ç”¨ã„ã¦ãƒªãƒ•ã‚¡ã‚¯ã‚¿ãƒªãƒ³ã‚°ã‚’è‡ªå‹•åŒ–ã™ã‚‹æ–¹æ³•"
type:        story
date:        2023-07-15
draft:       false
promote:     true
sticky:      false
url:         automate-refactoring-with-bazel-and-scalafix
tags:        [ "bazel", "scala" ]
---

### Scalafix ã«ã¤ã„ã¦

ã‚³ãƒ¼ãƒ‰ãƒ™ãƒ¼ã‚¹ãŒå¤§å‹åŒ–ã™ã‚‹ã«ã¤ã‚Œã€è‡ªå‹•ãƒªãƒ•ã‚¡ã‚¯ã‚¿ãƒªãƒ³ã‚°ã‚’è¡Œã†ã“ã¨ãŒã§ãã‚‹è¨€èªãƒ„ãƒ¼ãƒ«ãŒã‚ã‚‹ã¨ä¾¿åˆ©ã ã€‚å¹¸ã„ãªã“ã¨ã«ã€2016å¹´ã« Scala Center ãŒ [Scalafix][scalafix] ã‚’ä½œã£ã¦ãã‚ŒãŸã€‚[å…¬é–‹æ™‚ã®ãƒ–ãƒ­ã‚°è¨˜äº‹](https://www.scala-lang.org/blog/2016/10/24/scalafix.html)ã®ä¸­ã§ Ã“lafur Geirsson ã•ã‚“ã¯:

> Scalafix ã¯ã€ç°¡å˜ã‹ã‚‚ã—ã‚Œãªã„ãŒå˜èª¿ã«ç¹°ã‚Šè¿”ã•ã‚Œã‚‹ã‚³ãƒ¼ãƒ‰å¤‰æ›ã‚’å—ã‘æŒã¤ã“ã¨ã§ã€ã‚ãªãŸãŒæ„è­˜ã‚’å‘ã‘ã‚‹ä¾¡å€¤ã®ã‚ã‚‹ã“ã¨ã«é›†ä¸­ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚å¤§ã¾ã‹ã«èª¬æ˜ã™ã‚‹ã¨ã€Scalafix ã¯ã‚½ãƒ¼ã‚¹ã‚’èª­ã‚“ã§ã€éæ¨å¥¨æ©Ÿèƒ½ã®ä½¿ç”¨ã‚’æ–°ã—ã„ä»£æ›¿ã¸ã¨å¤‰æ›ã—ã€å…ƒã®ã‚½ãƒ¼ã‚¹ã«æ›¸ãè¾¼ã¿ã¾ã™ã€‚

ã¨è§£èª¬ã—ã¦ã„ã¦ã€Scala 3 ãƒã‚¤ã‚°ãƒ¬ãƒ¼ã‚·ãƒ§ãƒ³ãŒå‹•æ©Ÿã«ãªã£ã¦ã„ãŸã“ã¨ãŒã†ã‹ãŒãˆã‚‹ã€‚

ç¾åœ¨ã¯ã€Scalafix ã¯ Brice Jaglin ã•ã‚“ã‚‰ã«ã‚ˆã£ã¦ãƒ¡ãƒ³ãƒ†ã•ã‚Œã¦ã„ã¦ã€Scala 3 ãƒã‚¤ã‚°ãƒ¬ãƒ¼ã‚·ãƒ§ãƒ³ä»¥å¤–ã§ã‚‚ä¸€èˆ¬ã®ãƒªãƒ³ãƒ†ã‚£ãƒ³ã‚°ã‚„ãƒªãƒ•ã‚¡ã‚¯ã‚¿ãƒªãƒ³ã‚°ã®ãƒ„ãƒ¼ãƒ«ã¨ã—ã¦ä½¿ã‚ã‚Œã¦ã„ã‚‹ã€‚ä¾‹ãˆã°å‰ç”°ã•ã‚“ (xuwei-k) ãªã‚“ã‹ã¯æ•°ç™¾ã® Scalafix ã‚’æ›¸ã„ãŸã‚‰ã—ãã€ãã®ä¸€éƒ¨ã¯ [xuwei-k/scalafix-rules][scalafix-rules] ã«ã‚‚å…¬é–‹ã•ã‚Œã¦ã„ã‚‹ã€‚

Scalafix ç‹¬ç‰¹ã®ç‰¹å¾´ã¨ã—ã¦ã€syntactic (æ§‹æ–‡çš„) ã¨ semantic (æ„å‘³è«–çš„) ã¨ã„ã†2ç¨®é¡ã®ãƒ«ãƒ¼ãƒ«ãŒã‚ã‚‹ã€‚

- syntactic rule ã¯ã€ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã™ã‚‹ã“ã¨ãªãã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã«å¯¾ã—ã¦ç›´æ¥å®Ÿè¡Œã™ã‚‹ã“ã¨ãŒã§ãã‚‹ã€‚ã‚·ãƒ³ãƒ—ãƒ«ã ãŒã€ã‚³ãƒ¼ãƒ‰è§£æã®åŠ›ã«ã¯åˆ¶é™ãŒã‚ã‚‹ã€‚
- semantic rule ã¯ã€ã‚·ãƒ³ãƒœãƒ«ã‚„å‹ã‚’ç”¨ã„ã¦ã‚ˆã‚Šé«˜åº¦ãªã‚³ãƒ¼ãƒ‰è§£æã‚’è¡Œã†ã“ã¨ãŒã§ãã‚‹ãŒã€å…¥åŠ›ã‚½ãƒ¼ã‚¹ã‚’ SemanticDB ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ãƒ»ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã¨å…±ã«ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã—ãŸã‚‚ã®ã‚’äº‹å‰ã«ç”¨æ„ã™ã‚‹å¿…è¦ãŒã‚ã‚‹ã€‚

syntactic rule ã¯ Scalafix CLI ã•ãˆã‚ã‚Œã°è‰¯ã„ã®ã§ã€Bazel ã¨ã®çµ±åˆã¯ç‰¹ã«å¿…è¦ç„¡ã„ã€‚ä¸€æ–¹ã§ã€semantic rule ã¯ semanticdb ãªã©ã‚’æ¸¡ã—ã¦å›ã‚‹ãŸã‚ã€å°‘ã—ä½œæ¥­ãŒå¿…è¦ã¨ãªã‚‹ã€‚

### Bazel çµ±åˆã®å…ˆè¡Œç ”ç©¶

- [ianoc](https://github.com/ianoc) ã•ã‚“ãŒä½œã£ãŸ [ianoc/bazel-scalafix](https://github.com/ianoc/bazel-scalafix) ã¨ã„ã†ãƒªãƒãŒã‚ã‚‹ãŒã€Bash æˆåˆ†ãŒå¤šã„ã€‚æœ¬ç¨¿ã§ã¯ã€ã‚‚ã†å°‘ã— Starlark ã‚’ä½¿ã£ãŸæ–¹æ³•ã‚’è§£èª¬ã™ã‚‹ãŒã€Bash ã‚‚å¤šå°‘ã¯å¿…è¦ã¨ãªã‚‹ã€‚
- [Bazel ã‚’ç”¨ã„ã¦ä½•ã§ã‚‚ã‚¯ãƒ­ã‚¹ãƒ“ãƒ«ãƒ‰ã™ã‚‹æ–¹æ³•](/ja/cross-build-anything-with-bazel/)ã‚‚å‚ç…§ã€‚

### Bazel + Scalafix

æ‰‹é †ã®æ¦‚è¦

1. `ch.epfl.scala:scalafix-cli_<scalaVersion>:something`ã‚’è§£æ±ºã™ã‚‹
2. `org.scalameta:semanticdb-scalac_<scalaVersion>:4.8.4`ã‚’è§£æ±ºã™ã‚‹
3. Scalafix CLI ã‚’ãƒ©ãƒƒãƒ—ã—ãŸ `scala_binary` ã®ã‚¿ãƒ¼ã‚²ãƒƒãƒˆã‚’å®šç¾©ã™ã‚‹
4. Scalafix CLI ã«é©åˆ‡ãªã‚¤ãƒ³ãƒ—ãƒƒãƒˆã‚’æ¸¡ã—ã¦å®Ÿè¡Œã™ã‚‹ `scalafix(...)` ã‚«ã‚¹ã‚¿ãƒ ãƒ»ãƒ«ãƒ¼ãƒ«ã‚’å®šç¾©ã™ã‚‹
5. `upstream_scala_library(...)`, `semanticdb(...)`, `scalafix(...)` ã«å±•é–‹ã™ã‚‹ `scala_library(...)` ãƒã‚¯ãƒ­ã‚’å®šç¾©ã™ã‚‹
6. scalacOptions ã‚’æ¸¡ã™ãŸã‚ã®ã‚«ã‚¹ã‚¿ãƒ ãƒ„ãƒ¼ãƒ«ãƒã‚§ã‚¤ãƒ³ã‚’å®šç¾©ã™ã‚‹
7. å°ã•ã„ Bash ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’æ›¸ã

### 3rdparty ä¾å­˜æ€§ã®è§£æ±º

3rdparty ä¾å­˜æ€§ã®è§£æ±ºã«ã¯ç¾åœ¨ã„ãã¤ã‹ã®æ–¹æ³•ãŒã‚ã£ã¦ã€ã©ã‚Œã‚’é¸ã‚“ã§ã‚‚æ§‹ã‚ãªã„ã€‚åƒ•ãŒä»Šã®ã¨ã“ã‚è©¦ã—ãŸã®ã¯ [bazel-deps][bazel-deps] ã¨ `MODULE.bazel` ã§ä¸¡æ–¹ã¨ã‚‚å‹•ã„ãŸã€‚ä»¥ä¸‹ã¯ Bzlmod ã‚’ä½¿ã£ãŸä¾‹ã‹ã‚‰ã®æŠœç²‹ã :

#### MODULE.bazel

ãƒãƒ¼ã‚¸ãƒ§ãƒ³ç•ªå·ã¯ã€ã©ã® Scala ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã‚’ä½¿ã£ã¦ã„ã¦ãã‚Œã‚‰ã«å¯¾ã—ã¦ã‚¢ãƒ¼ãƒ†ã‚£ãƒ•ã‚¡ã‚¯ãƒˆãŒå…¬é–‹ã•ã‚Œã¦ã„ã‚‹ã‹ã«ã‚ˆã‚‹ã€‚

```python
bazel_dep(name = "mod_scala_multiverse")
local_path_override(
  module_name="mod_scala_multiverse",
  path="tools/local_modules/default",
)

maven = use_extension("@mod_scala_multiverse//:extensions.bzl", "maven")

maven.install(
    artifacts = [
        "ch.epfl.scala:::scalafix-cli:0.11.0",
        "org.scalameta:::semanticdb-scalac:4.8.4",
        ....
    ],
)
use_repo(maven, "maven")
```

#### 3rdparty/jvm/BUILD.bazel

ãã‚Œãã‚Œã®ä¾å­˜æ€§è§£æ±ºã®æ–¹æ³•ã«ã‚ˆã‚‹ç´°ã‹ãªé•ã„ã‚’å¸åã™ã‚‹ãŸã‚ã«ã“ã‚Œã‚‰ã®ä¾å­˜æ€§ã®ã‚¨ã‚¤ãƒªã‚¢ã‚¹ã‚’å®šç¾©ã™ã‚‹:

```python
load("@scala_multiverse//:cross_scala_config.bzl", "maven_dep")

alias(
    name = "ch_epfl_scala__scalafix-cli",
    actual = maven_dep("ch.epfl.scala:::scalafix-cli"),
    visibility = ["//visibility:public"],
)

alias(
    name = "org_scalameta__semanticdb_scalac",
    actual = maven_dep("org.scalameta:::semanticdb.scalac"),
    visibility = ["//visibility:public"],
)
```

ã“ã‚Œã«ã‚ˆã£ã¦ã€Scalafix ã‚’ `maven_dep("ch.epfl.scala:::scalafix-cli")` ã§ã¯ãªã `//3rdparty/jvm:ch_epfl_scala__scalafix-cli` ã¨ã—ã¦å‚ç…§ã§ãã‚‹ã€‚

### Scalafix ã® shim ã‚’æ›¸ã

JAR ã‚’ãã®ã¾ã¾ã§ã¯å®Ÿè¡Œã§ããªã„ã®ã§ã€`scala_binary` ã‚’ä½¿ã£ã¦ç°¡å˜ãª shim (èª¿æ•´æ¿) ã‚’ä½œã‚‹ã€‚

#### tools/scalafix_app/BUILD.bazel

```python
load("@io_bazel_rules_scala//scala:scala.bzl", "scala_binary")

scala_binary(
    name = "bin",
    srcs = glob(include = ["*.scala"]),
    main_class = "com.example.tools.scalafix_app.Main",
    visibility = ["//visibility:public"],
    deps = ["//3rdparty/jvm:ch_epfl_scala__scalafix-cli"],
)
```

#### tools/scalafix_app/Main.scala

```scala
package com.example.tools.scalafix_app

object Main extends App {
  scalafix.cli.Cli.main(args)
}
```

#### demo 1

`bazel run` ã‚’ä½¿ã£ã¦ Scala CLI ã‚’å‘¼ã³å‡ºã—ã¦ã¿ã‚‹:

```bash
$ bazel run tools/scalafix_app:bin -- -version
INFO: Analyzed target //tools/scalafix_app:bin (0 packages loaded, 0 targets configured).
INFO: Found 1 target...
Target //tools/scalafix_app:bin up-to-date:
  bazel-bin/tools/scalafix_app/bin.jar
  bazel-bin/tools/scalafix_app/bin
INFO: Elapsed time: 0.155s, Critical Path: 0.00s
INFO: 1 process: 1 internal.
INFO: Build completed successfully, 1 total action
INFO: Running command line: bazel-bin/tools/scalafix_app/bin -version
0.11.0
```

æ³¨æ„: `bazel run` ã¯éš”é›¢ã•ã‚ŒãŸã‚µãƒ³ãƒ‰ãƒœãƒƒã‚¯ã‚¹å†…ã§å®Ÿè¡Œã•ã‚Œã‚‹è¨­è¨ˆãªã®ã§ã€ã“ã®ã‚¿ãƒ¼ã‚²ãƒƒãƒˆã¯ã“ã®ã¾ã¾ã§ã¯ `*.scala` ãƒ•ã‚¡ã‚¤ãƒ«ã®å‡¦ç†ã‚’ã™ã‚‹ã“ã¨ãŒã§ããªã„ã€‚

### `scalafix(...)` ã®ãŸã‚ã®ã‚«ã‚¹ã‚¿ãƒ ãƒ»ãƒ«ãƒ¼ãƒ«

Bazel ç”¨èªã§ã€ãƒ«ãƒ¼ãƒ«ã¯å‘¼ã³å‡ºã™ã¨ã€sbt ã‚„ Gradle ã«ãŠã‘ã‚‹ã‚µãƒ–ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆç›¸å½“ã®ãƒ«ãƒ¼ãƒ«ãƒ»ã‚¿ãƒ¼ã‚²ãƒƒãƒˆã‚’ä½œã‚‹ã“ã¨ãŒã§ãã‚‹ç‰¹æ®Šãªé–¢æ•°ã®ã“ã¨ã ã€‚

ã‚¿ã‚¹ã‚¯ã‚’å®šç¾©ã™ã‚‹ä»£ã‚ã‚Šã«ã€Bazel ã§ã¯æ–°ã—ã„ãƒ«ãƒ¼ãƒ«ã‚’å®šç¾©ã™ã‚‹ã“ã¨ã§æ§˜ã€…ãªã‚¢ã‚¯ã‚·ãƒ§ãƒ³ã‚’å®Ÿè¡Œã™ã‚‹ã€‚

#### tools/rules/scalafix/BUILD.bazel

```python
# blank file
```

#### tools/rules/scalafix/scalafix.bzl

ä»¥ä¸‹ã¯ã€Scalafix CLI ã«é©åˆ‡ãª `--files` ã¨ `--classpath` ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã‚’æ¸¡ã—ã¦å‘¼ã³å‡ºã™ Bash ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’ç”Ÿæˆã™ã‚‹ãƒ«ãƒ¼ãƒ«ã‚’å®šç¾©ã™ã‚‹ã€‚

ã“ã“ã§è£æŠ€ã¨ã—ã¦ä½¿ã£ã¦ã„ã‚‹ã®ã¯ `cd "$BUILD_WORKING_DIRECTORY"` ã®å‘¼ã³å‡ºã—ã§ã€ã“ã‚Œã«ã‚ˆã£ã¦ã“ã®ã‚¹ã‚¯ãƒªãƒ—ãƒˆã¯ã‚µãƒ³ãƒ‰ãƒœãƒƒã‚¯ã‚¹ã‚’çªç ´ã—ã¦ãƒ¯ãƒ¼ã‚¯ã‚¹ãƒšãƒ¼ã‚¹ã®ä¸Šæ›¸ãã‚’ã™ã‚‹ã“ã¨ãŒã§ãã‚‹ã‚ˆã†ã«ãªã‚‹ã€‚

```python
def _scalafix_impl(ctx):
    out = ctx.actions.declare_file(ctx.label.name + ".sh")
    tool = ctx.executable._scalafix_bin
    tool_rf = ctx.attr._scalafix_bin[DefaultInfo]
    srcs = ctx.files.srcs
    semanticdb = ctx.attr.semanticdb[DefaultInfo]
    deps = semanticdb.files.to_list()
    script = """SANDBOX_DIR=$(pwd)
JARS=""
JARS0=({jars})
for JAR in ${{JARS0[@]}}; do
  JARS="$JARS --classpath $SANDBOX_DIR/$JAR"
done
cd "$BUILD_WORKING_DIRECTORY"
exec "$SANDBOX_DIR/{tool}" --files {srcs} $JARS --scalac-options -Xlint $@
""".format(
        tool = tool.short_path,
        srcs = " --files ".join([src.short_path for src in srcs]),
        jars = " ".join([x.short_path for x in deps])
    )
    ctx.actions.write(out, script, is_executable = True)
    files = srcs + deps
    rf = ctx.runfiles(
        transitive_files = depset(files)
    ).merge(tool_rf.default_runfiles)
    return [DefaultInfo(
        executable = out,
        runfiles = rf,
    )]

scalafix = rule(
    attrs = {
        "srcs": attr.label_list(
            allow_files = [".scala"],
        ),
        "semanticdb": attr.label(),
        "_scalafix_bin": attr.label(
            executable = True,
            cfg = "host",
            allow_files = True,
            default = Label("//tools/scalafix_app:bin"),
        ),
    },
    doc = "Runs scalafix",
    executable = True,
    implementation = _scalafix_impl,
)
```

ã“ã®ãƒ«ãƒ¼ãƒ«ã¯ 3ã¤ã®å¼•æ•°ã‚’å—ã‘å–ã‚Šã€ãã®ã†ã¡ã® 1ã¤ã¯ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆå€¤ã‚’æŒã¤ã€‚ã•ã‚‰ã«å…¨ã¦ã®ãƒ«ãƒ¼ãƒ«ã¯ `name` ã‚’æŒã¡ã€`visibility` ã‚„ `tags` ãªã©ã‚’æ¸¡ã™ã“ã¨ã‚‚ã§ãã‚‹ã€‚

### ã‚«ã‚¹ã‚¿ãƒ  `scala_library` ãƒã‚¯ãƒ­

Bazel ç”¨èªã§ã€ãƒã‚¯ãƒ­ã¯ç´”ç²‹é–¢æ•°ã§é€šå¸¸ä»–ã®ãƒ«ãƒ¼ãƒ«ã‚’å‘¼ã¶å‡ºã™ãŸã‚ã«ä½¿ã‚ã‚Œã‚‹ã€‚ãƒã‚¯ãƒ­ã®åå‰ãªã©ã¯ `bazel query` ãªã©ã§ã¯è¦‹ãˆãªããªã‚‹ã¨ã„ã†æ„å‘³ã§ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã™ã‚‹ã¨ç„¡ããªã‚‹ ("compile away" ã™ã‚‹) ã¨è¨€ãˆã‚‹ã€‚

[rules_scala][rules_scala] ãŒæä¾›ã™ã‚‹ `scala_library` ãƒ«ãƒ¼ãƒ«ã‚’ãã®ã¾ã¾ä½¿ã†ã®ãŒæ™®é€šã ãŒã€`scala_library` ã‚’ãƒã‚¯ãƒ­ã¨ã—ã¦å®šç¾©ã—ãªãŠã—ã¦ã€`scala_library(...)` ã‚’ 3ã¤ã®ã‚¿ãƒ¼ã‚²ãƒƒãƒˆã¸ã¨åˆ†å²ã•ã›ã‚‹ã“ã¨ãŒã§ãã‚‹ã€‚

#### tools/rules/scala/BUILD.bazel

```python
# blank file
```

#### tools/rules/scala/scala.bzl

```python
load(
    "@io_bazel_rules_scala//scala:scala.bzl",
    upstream_scala_library = "scala_library",
)
load(
    "@io_bazel_rules_scala//scala/unstable:defs.bzl",
    "make_scala_library",
)
load(
    "//tools/rules/scalafix:scalafix.bzl",
    "scalafix"
)

scala_semanticdb = make_scala_library()

def scala_library(
        name,
        srcs = [],
        deps = [],
        runtime_deps = [],
        plugins = [],
        data = [],
        resources = [],
        scalacopts = None,
        main_class = "",
        exports = [],
        resource_jars = [],
        visibility = None,
        javacopts = [],
        tags = []):
    upstream_scala_library(
        name = name,
        srcs = srcs,
        deps = deps,
        runtime_deps = runtime_deps,
        plugins = plugins,
        resources = resources,
        scalacopts = scalacopts,
        main_class = main_class,
        exports = exports,
        resource_jars = resource_jars,
        visibility = visibility,
        javacopts = [],
        tags = tags,
    )
    scalacopts_mod = scalacopts
    if scalacopts_mod and ("-Xfatal-warnings" in scalacopts_mod):
        scalacopts_mod.remove("-Xfatal-warnings")

    scala_semanticdb(
        name = "{}__semanticdb".format(name),
        srcs = srcs,
        deps = deps,
        runtime_deps = runtime_deps,
        plugins = plugins + ["//3rdparty/jvm:org_scalameta__semanticdb_scalac"],
        resources = resources,
        scalacopts = scalacopts_mod,
        main_class = main_class,
        exports = exports,
        resource_jars = resource_jars,
        visibility = visibility,
        javacopts = [],
        tags = tags + ["manual"],
    )
    scalafix(
        name = "{}__scalafix".format(name),
        srcs = srcs,
        semanticdb = "{}__semanticdb".format(name),
        tags = tags + ["manual"],
    )
```

### ã‚«ã‚¹ã‚¿ãƒ ãƒ»ãƒ„ãƒ¼ãƒ«ãƒã‚§ã‚¤ãƒ³

`scala_toolchain` ã‚’ã‚«ã‚¹ã‚¿ãƒ åŒ–ã™ã‚‹ã“ã¨ã§å…¨ã¦ã®ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã«ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§ `-deprecation` ã¨ `-Xlint` ãŒä»˜ãã‚ˆã†ã«ã™ã‚‹ã€‚å®Ÿã¯ Scalafix ãƒ«ãƒ¼ãƒ«ã®ã„ãã¤ã‹ã¯ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©ã®è­¦å‘Šã‚’å†åˆ©ç”¨ã—ã¦ã„ã‚‹ã®ã§ã€ã“ã‚Œã¯å¿…è¦ä¸å¯æ¬ ãªã‚¹ãƒ†ãƒƒãƒ—ã ã€‚

#### toolchains/BUILD.bazel

```python
load(
    "@io_bazel_rules_scala//scala:scala.bzl",
    "setup_scala_toolchain",
)

setup_scala_toolchain(
    name = "scala_toolchain",
    scala_compile_classpath = [
        "@maven//:org_scala_lang_scala_compiler",
        "@maven//:org_scala_lang_scala_library",
        "@maven//:org_scala_lang_scala_reflect",
    ],
    scala_library_classpath = [
        "@maven//:org_scala_lang_scala_library",
        "@maven//:org_scala_lang_scala_reflect",
    ],
    scala_macro_classpath = [
        "@maven//:org_scala_lang_scala_library",
        "@maven//:org_scala_lang_scala_reflect",
    ],
    scalacopts = [
        "-Yrangepos",
        "-deprecation",
        "-Xlint",
        "-feature",
        "-language:existentials",
        "-language:higherKinds",
    ],
    visibility = ["//visibility:public"]
)
```

#### WORKSPACE

```diff
# remove these
-load("@io_bazel_rules_scala//scala:toolchains.bzl", "scala_register_toolchains")
-scala_register_toolchains()

register_toolchains("//toolchains:scala_toolchain")
```

#### demo 2

```bash
$ bazel query common-test/src/main/scala/gigahorsetest/...
//common-test/src/main/scala/gigahorsetest:gigahorsetest
//common-test/src/main/scala/gigahorsetest:gigahorsetest__scalafix
//common-test/src/main/scala/gigahorsetest:gigahorsetest__semanticdb
Loading: 0 packages loaded
```

ã“ã‚Œã§ 3ã¤ã®ã‚¿ãƒ¼ã‚²ãƒƒãƒˆãŒä½œã‚‰ã‚Œã‚‹ã“ã¨ãŒåˆ†ã‹ã‚‹ã€‚

### ä¾¿åˆ©ã‚¹ã‚¯ãƒªãƒ—ãƒˆ

ä»¥ä¸‹ã¯ `bazel run` ã‚’å‘¼ã³å‡ºã™ãŸã‚ã®ã‚¹ã‚¯ãƒªãƒ—ãƒˆã ã€‚

#### bin/scalafix

```bash
#!/usr/bin/env bash

set -o errexit  # abort on nonzero exitstatus
set -o nounset  # abort on unbound variable
set -o pipefail # don't hide errors within pipes

BUILD_TARGET_QUERY=${1:-}
if [[ -z "$BUILD_TARGET_QUERY" ]]; then
  echo "usage: $0 <query> --rules <scalafix rules>"
  exit 1
fi
shift

SCALAFIX_TARGETS=$(bazel query "kind('scalafix', $BUILD_TARGET_QUERY)")
for TARGET in $SCALAFIX_TARGETS; do
  echo "processing $TARGET"
  bazel run "$TARGET" -- $@
done
```

#### demo 3

ä¸€å¿œ Coursier ã§ã‚·ã‚¹ãƒ†ãƒ ãƒ»ãƒ‘ã‚¹ã«ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã§ãã‚‹ `scalafix` ã‚’ä½¿ã£ã¦ syntactic ãƒ«ãƒ¼ãƒ«ã®ä½¿ã„æ–¹ã‹ã‚‰è¦‹ã¦ã„ãã€‚Scala ã«ãŠã„ã¦ä»¥ä¸‹ã®ã‚ˆã†ãªãƒ—ãƒ­ã‚·ãƒ¼ã‚¸ãƒ£æ§‹æ–‡ã¯éæ¨å¥¨ã«ãªã£ã¦ã„ã¦ ([#6325 ã§åƒ•ãŒä¸€èˆ¬çš„ã«éæ¨å¥¨ã«ã—ãŸ][6325])ã€ProcedureSyntax ãƒ«ãƒ¼ãƒ«ã‚’ä½¿ã£ã¦æ›¸ãæ›ãˆã‚‹ã“ã¨ãŒã§ãã‚‹:

```scala
def close()
```

```scala
def close(): Unit
```

```bash
$ cs install scalafix

$ scalafix core/src/main/scala/gigahorse/FullResponse.scala --rules ProcedureSyntax

$ rg -tscala 'def close' core
core/src/main/scala/gigahorse/FullResponse.scala
27:  def close(): Unit

core/src/main/scala/gigahorse/HttpClient.scala
26:  def close(): Unit
```

`*.scala` ãƒ•ã‚¡ã‚¤ãƒ«ã‚’æ¸¡ã™ã ã‘ã§ã„ã„ã®ã§ã€syntactic ãƒ«ãƒ¼ãƒ«ã‚’å®Ÿè¡Œã™ã‚‹ã®ã« Bazel çµ±åˆã¯å¿…è¦ç„¡ã„ã€‚

#### demo 4

æ¬¡ã«ã€æœªä½¿ç”¨ã® import æ–‡ã‚’é™¤å»ã™ã‚‹ semantic ãƒ«ãƒ¼ãƒ«ã®ä¾‹ã‚’ã¿ã¦ã¿ã‚‹ã€‚

```scala
package gigahorse

import java.nio.ByteBuffer
import scala.collection.mutable.Stack

abstract class FullResponse {
  def bodyAsByteBuffer: ByteBuffer
}
```

ä¸Šã®ä¾‹ã§ã¯ã€`import scala.collection.mutable.Stack` ã¯æœªä½¿ç”¨ã ã€‚

```bash
$ bin/scalafix //core/... --rules RemoveUnused
```

ã“ã‚Œã§æœªä½¿ç”¨ã® import æ–‡ãŒé™¤å»ã•ã‚ŒãŸã€‚

#### demo 5

ã¾ãŸã€çµæ§‹è‰¯ãä½¿ã‚ã‚Œã¦ã„ã‚‹ã¨æ€ã† Scalafix çµ„ã¿è¾¼ã¿ã®ãƒ«ãƒ¼ãƒ«ã§ OrganizeImports ã¨ã„ã†ã®ãŒã‚ã‚‹ã€‚

```bash
$ bin/scalafix ... --rules OrganizeImports
```

ã“ã‚Œã¯å…¨ã¦ã® import æ–‡ã‚’å„è¡Œ1ã‚¢ã‚¤ãƒ†ãƒ ã¥ã¤ã«å±•é–‹ã—ã¦ã€ã‚¢ãƒ«ãƒ•ã‚¡ãƒ™ãƒƒãƒˆé †ã«ä¸¦ã¹æ›¿ãˆã‚‹ã¨ã„ã†ã‚‚ã®ã :

```diff
diff --git a/core/src/main/scala/gigahorse/HttpClient.scala b/core/src/main/scala/gigahorse/HttpClient.scala
index fa08735..891e07f 100644
--- a/core/src/main/scala/gigahorse/HttpClient.scala
+++ b/core/src/main/scala/gigahorse/HttpClient.scala
@@ -16,8 +16,9 @@

 package gigahorse

-import scala.concurrent.{ Future, ExecutionContext }
 import java.io.File
+import scala.concurrent.ExecutionContext
+import scala.concurrent.Future
```

å€‹äººçš„ã«ã“ã®çµæœãŒè‰¯ã„ã®ã‹ã¯ç–‘å•ã ãŒã€åˆ©ç‚¹ã¨ã—ã¦ã¯è‡ªå‹•åŒ–ã§ãã‚‹ãŸã‚ã€ã‚³ãƒ¼ãƒ‰ãƒ¬ãƒ“ãƒ¥ãƒ¼æ™‚ã«ä¸æ¯›ãªè­°è«–ã‚’ã™ã‚‹ç‚¹ 1ã¤æ¸›ã‚‹ã¨ã„ã†ã“ã¨ãŒè¨€ãˆã‚‹ã€‚

### CI ã§ã® Scalafix ãƒ«ãƒ¼ãƒ«ã®æ¤œæŸ»

CI ä¸Šã§ Scalafix ãƒ«ãƒ¼ãƒ«ç¾¤ã‚’æ¤œæŸ»ã—ã¦ãƒªãƒ³ã‚¿ãƒ¼ã¨ã—ã¦ä½¿ã†ã«ã¯ã€`.scalafix.conf` ã«ãƒ«ãƒ¼ãƒ«ã‚’åˆ—æŒ™ã—ã¦ã€`bin/scalafix ... --check` ã‚’å‘¼ã³å‡ºã™ã€‚

#### .scalafix.conf

```python
rules = [
  DisableSyntax,
  OrganizeImports,
  RemoveUnused,
]
```

#### demo 6

```bash
$ bin/scalafix ... --check
```

ä»¥ä¸‹ã¯å®Ÿéš›ã® GitHub Actions [ãƒ­ã‚°](https://github.com/eed3si9n/gigahorse/actions/runs/5564808250/jobs/10164623980)ã‹ã‚‰æŠœç²‹ã—ãŸã‚‚ã®ã :

```bash
processing //common-test/src/main/scala/gigahorsetest:gigahorsetest__scalafix
Loading: 
Loading: 0 packages loaded
Analyzing: target //common-test/src/main/scala/gigahorsetest:gigahorsetest__scalafix (0 packages loaded, 0 targets configured)
INFO: Analyzed target //common-test/src/main/scala/gigahorsetest:gigahorsetest__scalafix (0 packages loaded, 44 targets configured).
INFO: Found 1 target...
....
Target //common-test/src/main/scala/gigahorsetest:gigahorsetest__scalafix up-to-date:
  bazel-bin/common-test/src/main/scala/gigahorsetest/gigahorsetest__scalafix.sh
INFO: Elapsed time: 13.906s, Critical Path: 7.07s
INFO: 46 processes: 9 internal, 35 linux-sandbox, 2 worker.
INFO: Build completed successfully, 46 total actions
INFO: Running command line: bazel-bin/common-test/src/main/scala/gigahorsetest/gigahorsetest__scalafix.sh --check
--- /home/runner/work/gigahorse/gigahorse/common-test/src/main/scala/gigahorsetest/BaseHttpClientSpec.scala
+++ <expected fix>
@@ -16,18 +16,20 @@
 
 package gigahorsetest
 
+import gigahorse.HeaderNames
+import gigahorse.MimeTypes
+import gigahorse.SignatureCalculator
+import gigahorse.WebSocketEvent
 import org.scalatest.Assertion
 import org.scalatest.flatspec.AsyncFlatSpec
 import org.scalatest.matchers.should.Matchers
+import unfiltered.netty.Server

Error: Process completed with exit code 32.
```

#### å®Ÿç”¨ä¾‹

ã“ã‚Œã‚‰ã‚’ã¾ã¨ã‚ãŸå®Ÿç”¨ä¾‹ã¯ <https://github.com/eed3si9n/gigahorse/pull/86> ã‚’å‚ç…§ã€‚

### ã¾ã¨ã‚

Scala ã‚¨ã‚³ã‚·ã‚¹ãƒ†ãƒ ã«ãŠã„ã¦ã€Scala Center ãŒé–‹ç™ºã—ãŸ Scalafix ã¯ãƒªãƒ•ã‚¡ã‚¯ã‚¿ãƒªãƒ³ã‚°ã‚„ãƒªãƒ³ãƒ†ã‚£ãƒ³ã‚°ã‚’è‡ªå‹•åŒ–ã™ã‚‹ãƒ„ãƒ¼ãƒ«ã‚’æä¾›ã™ã‚‹ã€‚å¤§è¦æ¨¡ã‚³ãƒ¼ãƒ‰ãƒ™ãƒ¼ã‚¹ã‚’ä½¿ã†ãƒ¦ãƒ¼ã‚¶ãƒ¼ã¯ Scalafix çµ„ã¿è¾¼ã¿ã‚‚ã—ãã¯ã‚³ãƒŸãƒ¥ãƒ‹ãƒ†ã‚£ãƒ¼ãŒãƒ¡ãƒ³ãƒ†ã—ã¦ã„ã‚‹ãƒ«ãƒ¼ãƒ«ç¾¤ã‚’ä½¿ã£ã¦åŠ¹ç‡çš„ã«ã‚³ãƒ¼ãƒ‰ã®æ›¸ãæ›ãˆã‚’è¡Œã†ã“ã¨ãŒã§ãã‚‹ã€‚

ã„ãã¤ã‹ã®ã‚«ã‚¹ã‚¿ãƒ åŒ–ã‚’è¡Œã†ã“ã¨ã§ã€Scalafix ã® semantic rule ã‚‚ Bazel ãƒ¢ãƒãƒªãƒã‹ã‚‰ä½¿ã†ã“ã¨ãŒã§ãã‚‹ã€‚

----

#### ãƒ©ã‚¤ã‚»ãƒ³ã‚¹

æ³•ä»¤ä¸Šèªã‚ã‚‰ã‚Œã‚‹æœ€å¤§é™ã®ç¯„å›²ã§ä½œè€…ã¯ã€æœ¬ç¨¿ã«ãŠã‘ã‚‹ã‚³ãƒ¼ãƒ‰ä¾‹ã®è‘—ä½œæ¨©ãŠã‚ˆã³è‘—ä½œéš£æ¥æ¨©ã‚’æ”¾æ£„ã—ã¦ã€å…¨ä¸–ç•Œã®ãƒ‘ãƒ–ãƒªãƒƒã‚¯ãƒ»ãƒ‰ãƒ¡ã‚¤ãƒ³ã«æä¾›ã—ã¦ã„ã‚‹ã€‚
ã‚³ãƒ¼ãƒ‰ä¾‹ã¯ä¸€åˆ‡ã®ä¿è¨¼ãªãå…¬é–‹ã•ã‚Œã‚‹ã€‚<http://creativecommons.org/publicdomain/zero/1.0/> å‚ç…§ã€‚

#### ğŸ³ï¸â€ğŸŒˆ ã‚¦ã‚¯ãƒ©ã‚¤ãƒŠã‚’ã‚µãƒãƒ¼ãƒˆã—ã‚ˆã† ğŸ‡ºğŸ‡¦

Forbidden Colours ã¯ã€ãƒãƒ¼ãƒ©ãƒ³ãƒ‰ã€ãƒãƒ³ã‚¬ãƒªãƒ¼ã€ãƒ«ãƒ¼ãƒãƒ‹ã‚¢ãªã©ã§ LGBTIQ+ é›£æ°‘ã‚’è£œåŠ©ã™ã‚‹ãŸã‚ã®å‹Ÿé‡‘ã‚­ãƒ£ãƒ³ãƒšãƒ¼ãƒ³ã‚’è¡Œã£ã¦ã„ã‚‹ã€‚

<https://www.forbidden-colours.com/2022/02/26/support-ukrainian-lgbtiq-refugees/>

#### Scala Center ã¸å¯„ä»˜ã—ã‚ˆã†

Scala Center ã¯ã€æ•™è‚²ã¨ã‚ªãƒ¼ãƒ—ãƒ³ã‚½ãƒ¼ã‚¹ã‚’è£œåŠ©ã™ã‚‹ã“ã¨ã‚’ç›®çš„ã¨ã—ãŸ EPFL å†…ã«ã‚ã‚‹éå–¶åˆ©å›£ä½“ã§ã€å€‹äººã‹ã‚‰ã®å‹Ÿé‡‘ã‚‚å—ã‘ä»˜ã‘ã¦ã„ã‚‹ã€‚

- <https://scala.epfl.ch/donate.html>

  [scalafix]: https://scalacenter.github.io/scalafix/
  [scalafix-rules]: https://github.com/xuwei-k/scalafix-rules
  [bazel-deps]: https://github.com/bazeltools/bazel-deps
  [rules_scala]: https://github.com/bazelbuild/rules_scala
  [6325]: https://github.com/scala/scala/pull/6325
